.include "./Supporting files/nominal.jsim"
.include "./Supporting files/stdcell.jsim"
.include "./Supporting files/2dcheckoff_3ns.jsim"

* Third design, working with tpd <3ns

**** Faster XOR ******************

.subckt betterxor a b z
Xz1 a b 1 nand2
Xz2 a 1 2 nand2
Xz3 b 1 3 nand2
Xz4 2 3 z nand2
.ends

**********************************
**** FA circuit ******************

.subckt FA a b cin s g p
Xs1 a b 1 betterxor
Xs2 cin 1 s betterxor
Xp a b p nor2
Xg a b g nand2
.ends

**********************************
**** V & Z signals ***************

.subckt Vsignal a b s v
Xvs s vs inverter
Xv0 a b v0 nand2
Xv1 v0 v1 inverter
Xv2 v1 vs v2 nand2
Xv3 a b v3 nor2
Xv4 v3 s v4 nand2
Xv5 v2 v4 v nand2
.ends

.subckt Zsignal s[31:0] z
Xz[15:0] s[31:16] s[15:0] z[15:0] nor2
Xz[23:16] z[15:8] z[7:0] z[23:16] nand2
Xz[27:24] z[23:20] z[19:16] z[27:24] nor2
Xz[29:28] z[27:26] z[25:24] z[29:28] nand2
Xz30 z29 z28 z nor2
.ends

**********************************
**** Lookahead Carry Unit ********

* Receives inverted inputs and produces non-inverted outputs
.subckt LCUI g[1:0] p[1:0] c g2 p2 c[1:0]
Xp0 p1 p0 p2 nor2

Xg0 p1 ga inverter
Xg1 g0 gb inverter
Xg2 ga gb gc nand2
Xg3 gc g1 g2 nand2

Xc0 p0 pa inverter
Xc1 c pa pb nand2
Xc2 g0 pb c1 nand2
Xc3 c c0 buffer
.ends

* Receives non-inverted inputs and produces inverted outputs
.subckt LCUN g[1:0] p[1:0] c g2 p2 c[1:0]
Xp0 p1 p0 p2 nand2

Xg0 g1 ga inverter
Xg1 g0 p1 gb nand2
Xg2 ga gb gc nand2
Xg3 gc g2 inverter

Xc0 g0 gd inverter
Xc1 p0 c ca nand2
Xc2 ca gd c1 nand2
Xc3 c c0 buffer
.ends

**********************************
**** Carry Lookahead *************

.subckt adder32 op0 a[31:0] b[31:0] s[31:0] z v n

Xbx[31:0] b[31:0] op0#32 bx[31:0] betterxor
Xfa[31:0] a[31:0] bx[31:0] c[31:0] s[31:0] g[31:0] p[31:0] FA

Xlcua0 g[1:0] p[1:0] ca0 ga0 pa0 c[1:0] LCUI
Xlcua1 g[3:2] p[3:2] ca1 ga1 pa1 c[3:2] LCUI
Xlcua2 g[5:4] p[5:4] ca2 ga2 pa2 c[5:4] LCUI
Xlcua3 g[7:6] p[7:6] ca3 ga3 pa3 c[7:6] LCUI
Xlcua4 g[9:8] p[9:8] ca4 ga4 pa4 c[9:8] LCUI
Xlcua5 g[11:10] p[11:10] ca5 ga5 pa5 c[11:10] LCUI
Xlcua6 g[13:12] p[13:12] ca6 ga6 pa6 c[13:12] LCUI
Xlcua7 g[15:14] p[15:14] ca7 ga7 pa7 c[15:14] LCUI
Xlcua8 g[17:16] p[17:16] ca8 ga8 pa8 c[17:16] LCUI
Xlcua9 g[19:18] p[19:18] ca9 ga9 pa9 c[19:18] LCUI
Xlcua10 g[21:20] p[21:20] ca10 ga10 pa10 c[21:20] LCUI
Xlcua11 g[23:22] p[23:22] ca11 ga11 pa11 c[23:22] LCUI
Xlcua12 g[25:24] p[25:24] ca12 ga12 pa12 c[25:24] LCUI
Xlcua13 g[27:26] p[27:26] ca13 ga13 pa13 c[27:26] LCUI
Xlcua14 g[29:28] p[29:28] ca14 ga14 pa14 c[29:28] LCUI
Xlcua15 g[31:30] p[31:30] ca15 ga15 pa15 c[31:30] LCUI

Xlcub0 ga[1:0] pa[1:0] cb0 gb0 pb0 ca[1:0] LCUN
Xlcub1 ga[3:2] pa[3:2] cb1 gb1 pb1 ca[3:2] LCUN
Xlcub2 ga[5:4] pa[5:4] cb2 gb2 pb2 ca[5:4] LCUN
Xlcub3 ga[7:6] pa[7:6] cb3 gb3 pb3 ca[7:6] LCUN
Xlcub4 ga[9:8] pa[9:8] cb4 gb4 pb4 ca[9:8] LCUN
Xlcub5 ga[11:10] pa[11:10] cb5 gb5 pb5 ca[11:10] LCUN
Xlcub6 ga[13:12] pa[13:12] cb6 gb6 pb6 ca[13:12] LCUN
Xlcub7 ga[15:14] pa[15:14] cb7 gb7 pb7 ca[15:14] LCUN

Xlcuc0 gb[1:0] pb[1:0] cc0 gc0 pc0 cb[1:0] LCUI
Xlcuc1 gb[3:2] pb[3:2] cc1 gc1 pc1 cb[3:2] LCUI
Xlcuc2 gb[5:4] pb[5:4] cc2 gc2 pc2 cb[5:4] LCUI
Xlcuc3 gb[7:6] pb[7:6] cc3 gc3 pc3 cb[7:6] LCUI

Xlcud0 gc[1:0] pc[1:0] cd0 gd0 pd0 cc[1:0] LCUN
Xlcud1 gc[3:2] pc[3:2] cd1 gd1 pd1 cc[3:2] LCUN

Xlcue0 gd[1:0] pd[1:0] op0 ge0 pe0 cd[1:0] LCUI

* n signal
.connect s31 n

* v signal
Xv a31 bx31 s31 v Vsignal

* z signal
Xz s[31:0] z Zsignal

.ends
**********************************
